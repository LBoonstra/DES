grammar rover.lego.Rdsl with org.eclipse.xtext.common.Terminals

generate rdsl "http://www.lego.rover/Rdsl"

// Code by Richard and Lisa. Take generated code (copy and paste generated text, not file) and place them in
// a code file that is in unix and text-file encoding UTF-8 (and is capable of running on the robot).
// We placed the focus on getting working DSL for the robot: for the marsrover, we will aim at making the above
// easier, preferably that generated code can immediately be uploaded. Validation code will be implemented for
// the MarsRover, but would only be of 'warning, this is not logical.'
// Suggestions for specs:
//Task: FollowColor: black speed: medium stopconditions: bumper object time 40
//Task: findColors: black red speed: fast stopconditions: color blue avoid: bumper
//Task: drive: right speed: medium stopconditions: object avoid: color black
Task: 'Task:' mission=Mission;

Mission: DriveMission | DoNothingMission |FindColorsMission |FollowLineMission;

DoNothingMission: 'DoNothing:' time = INT;

DriveMission: 'drive:' dir = DirectionOptions
	'speed:'speed = SpeedOptions
	'stopconditions:' stopcons += StopOptions*
	('avoid:' avoid = Obstacles)?
;

FindColorsMission: 'findColors:' findcolor += Colors*
	'speed:'speed = SpeedOptions
	'stopconditions:' stopcons += StopOptions*
	('avoid:' avoid = Obstacles)?
;

FollowLineMission: 'FollowColor:' followColor = Color
	'speed:'speed = SpeedOptions
	'stopconditions:' stopcons += StopOptions*
;

Colors: colors = Color
;
StopOptions:  Obstacles| TimeObject;

TimeObject: 'time' time= INT;

Obstacles:
	 UseObstacle |ColorWithName
;

ColorWithName: 'color' color = Color;

UseObstacle: OE= ObstaclesEnum;

enum ObstaclesEnum:
	BUMPER = 'bumper'|
	OBJECT = 'object'
	;

enum Color:
	YELLOW = 'yellow' |
	BLUE = 'blue'|
	RED = 'red'|
	BLACK = 'black'
;

enum SpeedOptions:
	NOT_MOVING = 'not_moving'|
	SLOW = 'slow'|
	MEDIUM = 'medium'|
	FAST = 'fast'
;

enum DirectionOptions:
	LEFT = 'left'|
	RIGHT = 'right'|
	FORWARD = 'forward'|
	BACKWARD = 'backward'|
	RANDOM = 'random'
;
